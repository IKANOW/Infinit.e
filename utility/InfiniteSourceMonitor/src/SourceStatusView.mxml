<?xml version="1.0" encoding="utf-8"?>
<!--
Copyright 2012, The Infinit.e Open Source Project

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
-->
<s:VGroup xmlns:fx="http://ns.adobe.com/mxml/2009"
		  xmlns:s="library://ns.adobe.com/flex/spark"
		  xmlns:mx="library://ns.adobe.com/flex/mx"
		  creationComplete="vgroup1_creationCompleteHandler(event)" xmlns:views="views.*">
	<fx:Script>
		<![CDATA[
			import com.adobe.serialization.json.JSONDecoder;
			import com.adobe.utils.StringUtil;
			
			import events.CommunityEvent;
			
			import mx.collections.ArrayCollection;
			import mx.collections.IList;
			import mx.collections.Sort;
			import mx.collections.SortField;
			import mx.controls.Alert;
			import mx.controls.DateField;
			import mx.core.FlexGlobals;
			import mx.events.CloseEvent;
			import mx.events.FlexEvent;
			import mx.events.FlexMouseEvent;
			import mx.events.ListEvent;
			import mx.formatters.DateFormatter;
			import mx.managers.PopUpManager;
			import mx.managers.PopUpManagerChildList;
			import mx.rpc.events.FaultEvent;
			import mx.rpc.events.ResultEvent;
			import mx.utils.ObjectUtil;
			
			import objects.Community;
			import objects.CommunityAttribute;
			
			import renderers.GridTextItemRenderer;
			
			import spark.components.Button;
			import spark.components.Label;
			import spark.events.TextOperationEvent;
			
			import util.Constants;
			import util.DateConverter;
			
			import views.Communities;
			
			private static var ENDPOINT_URL:String = flash.external.ExternalInterface.call( "getEndPointUrl" );
			
			[Bindable]
			
			[Embed( source = "assets/greencheck.png" )]
			public var greencheck:Class;
			
			[Bindable]
			
			[Embed( source = "assets/redx.png" )]
			public var redx:Class;
			
			[Bindable]
			
			[Embed( source = "assets/yellowexclamation.png" )]
			public var yellowexclamation:Class;
			
			[Bindable]
			private var currsourceData:ArrayCollection = new ArrayCollection();
			
			private var sourceData:ArrayCollection = new ArrayCollection();
			
			private var communitiesData:ArrayCollection = new ArrayCollection();
			
			private var communityData:ArrayCollection;
			
			private var timer:Timer = new Timer( 5000 );
			
			private var sortFlip:Boolean = false;
			
			private var sortLast:String = "";
			
			private var communitiesPopup:Communities;
			private var selectedCommunities:ArrayCollection;
			[Bindable]
			private var searchTerm:String;
			private var count:int = 0; 
			
			public function loadSourceData( communityInfo:ArrayCollection ):void
			{
				communityData = communityInfo;
				var commString:String = "";
				
				for each ( var o:Object in communityData )
				{
					commString += o._id + ",";
				}
				commString = commString.substring( 0, commString.length - 1 );
				goodSourceService.url = ENDPOINT_URL + "config/source/good/" + commString;
				goodSourceService.send();
				pendingSourceService.url = ENDPOINT_URL + "config/source/pending/" + commString;
				pendingSourceService.send();
			}
			
			public function loadCommunityData( communityInfo:ArrayCollection ):void
			{
				communitiesData = communityInfo;
			}
			
			public function loadSelectedCommunityData( selectedCommunities:ArrayCollection ):void
			{
				this.selectedCommunities = selectedCommunities;
			}
			
			protected function filter_enterHandler( event:TextOperationEvent ):void
			{
				var sort:Sort = currsourceData.sort as Sort;
				currsourceData.sort = null;
				currsourceData.removeAll();
				currsourceData.refresh();
				var filterPattern:RegExp = new RegExp( filterInput.text, "i" );
				
				for each ( var o:Object in sourceData )
				{
					if ( ( o._id as String ).search( filterPattern ) != -1 )
					{
						currsourceData.addItem( ObjectUtil.copy( o ) );
					}
					else if ( ( o.title as String ).search( filterPattern ) != -1 )
					{
						currsourceData.addItem( ObjectUtil.copy( o ) );
					}
					else if ( ( o.communityID as String ).search( filterPattern ) != -1 )
					{
						currsourceData.addItem( ObjectUtil.copy( o ) );
					}
					else if ( ( o.communityName as String ).search( filterPattern ) != -1 )
					{
						currsourceData.addItem( ObjectUtil.copy( o ) );
					}
					else if ( o.harvest != null && o.harvest.harvest_status != null && ( o.harvest.harvest_status as String ).search( filterPattern ) != -1 )
					{
						currsourceData.addItem( ObjectUtil.copy( o ) );
					}
					else if ( o.harvest != null && o.harvest.harvest_message != null && ( o.harvest.harvest_message as String ).search( filterPattern ) != -1 )
					{
						currsourceData.addItem( ObjectUtil.copy( o ) );
					}
				}
				currsourceData.sort = sort;
				currsourceData.refresh();
			}
			
			protected function filterTextInput_clearHandler():void
			{
				searchTerm = Constants.BLANK;
				sourceData.removeAll();
				currsourceData.removeAll();
				loadSourceData(selectedCommunities);
			}
			
			protected function filterTextInput_focusInHandler():void
			{
				if ( filterInput.text.length > 0 )
					filterInput.selectRange( filterInput.text.length, filterInput.text.length );
			}
			
			protected function goodSourceService_faultHandler( event:FaultEvent ):void
			{
				Alert.show( "Error: Could not contact api server to get good sources" );
			}
			
			protected function goodSourceService_resultHandler( event:ResultEvent ):void
			{
				var json:JSONDecoder = new JSONDecoder( event.result as String, true );
				var data:Object = json.getValue();
				var sourceItems:ArrayCollection = new ArrayCollection( data.data );
				
				var goodSources:ArrayCollection = new ArrayCollection();
				
				for each ( var o:Object in sourceItems )
				{
					o.communityName = setCommunityName( o.communityIds[ 0 ] );
					o.communityID = o.communityIds[ 0 ];
					
					if ( o.title == null )
						o.title = "";
					o.title = StringUtil.trim( o.title.toString() );
					
					if ( o.harvest == null || o.harvest.harvest_status == null || o.isApproved == false )
					{
						o.statusImage = yellowexclamation;
						o.statusColor = 0xEEEE00;
						o.harvestDate = null;
					}
					else if ( o.harvest.harvest_status == "success" )
					{
						o.statusImage = greencheck;
						o.statusColor = 0x00FF00;
						o.harvestDate = DateConverter.parseDate( o.harvest.harvested );
					}
					else
					{
						o.statusImage = redx;
						o.statusColor = 0xFF0000;
						o.harvestDate = DateConverter.parseDate( o.harvest.harvested );
					}
					
					if(!doesCollectionContainItem(sourceData, o))
						goodSources.addItem(o);
					else
					{
						replaceItemById(sourceData, o);
						replaceItemById(currsourceData, o);
					}
				}				
				
				sourceData.addAll( goodSources );
				currsourceData.addAll( goodSources ); 
				
				checkFilter();
			}
			
			
			protected function pendingSourceService_faultHandler( event:FaultEvent ):void
			{
				Alert.show( "Error: Could not contact api server to get pending sources" );
			}
			
			
			protected function pendingSourceService_resultHandler( event:ResultEvent ):void
			{
				var json:JSONDecoder = new JSONDecoder( event.result as String, true );
				var data:Object = json.getValue();
				var sourceItems:ArrayCollection = new ArrayCollection( data.data );
				var pendingSources:ArrayCollection = new ArrayCollection();
				
				for each ( var o:Object in sourceItems )
				{
					o.communityName = setCommunityName( o.communityIds[ 0 ] );
					o.communityID = o.communityIds[ 0 ];
					
					if ( o.title == null )
						o.title = "";
					o.title = StringUtil.trim( o.title.toString() );
					
					if ( o.harvest == null || o.harvest.harvest_status == null || o.isApproved == false )
					{
						o.statusImage = yellowexclamation;
						o.statusColor = 0xEEEE00;
						o.harvestDate = null;
					}
					else if ( o.harvest.harvest_status == "success" )
					{
						o.statusImage = greencheck;
						o.statusColor = 0x00FF00;
						o.harvestDate = DateConverter.parseDate( o.harvest.harvested );
					}
					else
					{
						o.statusImage = redx;
						o.statusColor = 0xFF0000;
						o.harvestDate = DateConverter.parseDate( o.harvest.harvested );
					}
					
					if(!doesCollectionContainItem(sourceData, o))
						pendingSources.addItem(o);
					else
					{
						replaceItemById(sourceData, o);
						replaceItemById(currsourceData, o);
					}
				}
				
				sourceData.addAll( pendingSources );
				currsourceData.addAll( pendingSources );
				
				checkFilter();
			}
			
			protected function sort_clickHandler( event:MouseEvent ):void
			{
				var buttonid:String = ( event.currentTarget as Button ).id;
				var sortField:SortField = new SortField();
				var sort:Sort = new Sort();
				
				if ( buttonid == "titleSortButton" )
				{
					sortField.name = "title";
					sortField.caseInsensitive = true;
				}
				else if ( buttonid == "dateSortButton" )
				{
					sortField.name = "harvestDate";
					sort.compareFunction = dateCompareFunction;
				}
				else if ( buttonid == "errorSortButton" )
				{
					sortField.name = "harvest.harvest_status";
					sort.compareFunction = errorCompareFunction;
				}
				else if ( buttonid == "idSortButton" )
				{
					sortField.name = "_id";
				}
				else if ( buttonid == "successSortButton" )
				{
					sortField.name = "harvest.harvest_status";
					sortField.compareFunction = statusCompareFunction;
				}
				else if ( buttonid == "messageSortButton" )
				{
					sortField.name = "harvest.harvest_message";
					sortField.compareFunction = messageCompareFunction
				}
				
				
				sortField.numeric = false;
				sort.fields = [ sortField ];
				
				if ( sortLast == buttonid )
				{
					sortFlip = !sortFlip;
				}
				else
				{
					sortFlip = false;
				}
				
				if ( sortFlip )
					sort.reverse();
				sortLast = buttonid;
				currsourceData.sort = sort;
				currsourceData.refresh();
			}
			
			protected function statusDG_itemDoubleClickHandler( event:MouseEvent ):void
			{
				if(event.target is Label && (event.target.owner is GridTextItemRenderer || event.target.owner.owner.owner.owner is errorLabelItemRenderer))
				{
					var columnInfo:GridTextItemRenderer = event.target.owner as GridTextItemRenderer;
					var col:GridColumn;
					
					if(columnInfo != null)
					 	col = statusDG.columns.getItemAt(columnInfo.columnIndex) as GridColumn;
					else
					{
						var colInfo:errorLabelItemRenderer = event.target.owner.owner.owner.owner as errorLabelItemRenderer;
						col = statusDG.columns.getItemAt(colInfo.columnIndex) as GridColumn;
					}
					var colname:Array = col.dataField.split( "." );
					var obj:Object = statusDG.selectedItem;
					
					for each ( var s:String in colname )
					{
						obj = obj[ s ];
					}
					var selectedData:String = " ";
					
					if ( obj != null )
						selectedData = obj.toString(); 
					System.setClipboard( selectedData );
					debugLabel.text = "Data cell copied to clipboard.";
					debugFadeOut.stop();
					debugFadeIn.stop();
					debugLabel.visible = true;
					timer.start();
				}
			}
			
			
			protected function statusDG_rollOverHandler( event:ListEvent ):void
			{
				statusDG.setStyle( "rollOverColor", event.itemRenderer.data.statusColor );
				statusDG.validateNow();
			}
			
			protected function timer_event( event:TimerEvent ):void
			{
				timer.stop();
				debugLabel.visible = false;
			}
			
			
			protected function vgroup1_creationCompleteHandler( event:FlexEvent ):void
			{
				timer.addEventListener( TimerEvent.TIMER, timer_event );
				statusDG.addEventListener( ListEvent.ITEM_ROLL_OVER, statusDG_rollOverHandler );
			}
			
			private function dateCompareFunction( objA:Object, objB:Object, fields:Array = null ):int
			{
				var result:int = ObjectUtil.dateCompare( objA.harvestDate, objB.harvestDate );
				
				//if you want null to show up as the largest date, comment out the next 2 lines
				if ( objA.harvestDate == null )
					result = -1;
				else if ( objB.harvestDate == null )
					result = 1;
				
				if ( fields != null && fields.length > 0 && fields[ 0 ].descending )
					result *= -1;
				return result;
			}
			
			private function docCountCompareFunction( objA:Object, objB:Object, fields:Array = null ):int
			{
				var result:int = -1;
				
				if ( objA.harvest == null || objA.harvest.doccount == null )
					result = -1;
				else if ( objB.harvest == null || objB.harvest.doccount == null )
					result = 1;
				else
					result = ObjectUtil.numericCompare( objA.harvest.doccount, objB.harvest.doccount );
				
				if ( fields != null && fields.length > 0 && fields[ 0 ].descending )
					result *= -1;
				return result;
			}
			
			private function errorCompareFunction( objA:Object, objB:Object, fields:Array = null ):int
			{
				var result:int = -1;
				
				if ( ( objA.harvest == null || objA.harvest.harvest_status == null ) && ( objB.harvest == null || objB.harvest.harvest_status == null ) )
					result = 0;
				else if ( ( objA.harvest == null || objA.harvest.harvest_status == null ) && objB.harvest.harvest_status == "success" )
					result = -1;
				else if ( ( objA.harvest == null || objA.harvest.harvest_status == null ) && objB.harvest.harvest_status == "error" )
					result = 1;
				else if ( ( objB.harvest == null || objB.harvest.harvest_status == null ) && objA.harvest.harvest_status == "success" )
					result = 1;
				else if ( ( objB.harvest == null || objB.harvest.harvest_status == null ) && objA.harvest.harvest_status == "error" )
					result = -1;
				else if ( objA.harvest.harvest_status == objB.harvest.harvest_status )
					result = 0;
				else if ( objA.harvest.harvest_status == "success" )
					result = 1;
				else
					result = -1;
				
				if ( fields != null && fields.length > 0 && fields[ 0 ].descending )
					result *= -1;
				return result;
			}
			
			private function messageCompareFunction( objA:Object, objB:Object, fields:Array = null ):int
			{
				var result:int = -1;
				
				if ( objA.harvest == null || objA.harvest.harvest_message == null )
					result = -1;
				else if ( objB.harvest == null || objB.harvest.harvest_message == null )
					result = 1;
				else
					result = ObjectUtil.stringCompare( objA.harvest.harvest_message, objB.harvest.harvest_message, true );
				
				if ( fields != null && fields.length > 0 && fields[ 0 ].descending )
					result *= -1;
				return result;
			}
			
			private function setCommunityName( commid:String ):String
			{
				for each ( var o:Object in communityData )
				{
					if ( o._id == commid )
						return o.name;
				}
				return "Error: Couldn't find matching community.";
			}
			
			private function statusCompareFunction( objA:Object, objB:Object, fields:Array = null ):int
			{
				var result:int = -1;
				
				if ( objA.harvest == null || objA.harvest.harvest_status == null )
					result = -1;
				else if ( objB.harvest == null || objB.harvest.harvest_status == null )
					result = 1;
				else
					result = ObjectUtil.stringCompare( objA.harvest.harvest_status, objB.harvest.harvest_status, true );
				
				if ( fields != null && fields.length > 0 && fields[ 0 ].descending )
					result *= -1;
				return result;
			}
			
			public function closeCommunitiesPopup( event:Event = null ):void
			{
				if ( communitiesPopup )
				{
					communitiesPopup.removeEventListener( FlexMouseEvent.MOUSE_DOWN_OUTSIDE, communitiesPopup_clickOutsideHandler );
					communitiesPopup.removeEventListener( CloseEvent.CLOSE, closeCommunitiesPopup );
					PopUpManager.removePopUp( communitiesPopup );
					communitiesPopup = null;
					communitiesButton.selected = false;
				}
			}
			
			protected function communitiesButton_clickHandler( event:MouseEvent ):void
			{
				if ( communitiesPopup )
				{
					closeCommunitiesPopup();
				}
				else
				{
					communitiesPopup = PopUpManager.createPopUp( DisplayObject( FlexGlobals.topLevelApplication ), Communities, false, PopUpManagerChildList.APPLICATION ) as Communities;
					communitiesPopup.setCommunities(communitiesData);
					communitiesPopup.addEventListener(CommunityEvent.COMMUNITY_JOINED, handleCommunityJoinedEvent);
					communitiesPopup.addEventListener(CommunityEvent.SELECT_COMMUNITY, handleSelectCommunityEvent);
					communitiesPopup.addEventListener( FlexMouseEvent.MOUSE_DOWN_OUTSIDE, communitiesPopup_clickOutsideHandler, false, 0, true );
					communitiesPopup.addEventListener( CloseEvent.CLOSE, closeCommunitiesPopup, false, 0, true );
					
					positionCommunitiesPopup();
				}
			}
			
			private function handleCommunityJoinedEvent(event:CommunityEvent):void
			{
				dispatchEvent(event);
			}
			
			protected function communitiesPopup_clickOutsideHandler( event:Event ):void
			{
				if ( event is MouseEvent && communitiesPopup )
				{
					var mouseEvent:MouseEvent = MouseEvent( event );
					
					if ( mouseEvent.target != communitiesPopup || mouseEvent.relatedObject == communitiesButton )
					{
						return;
					}
					
					if ( !communitiesPopup.hitTestPoint( mouseEvent.stageX, mouseEvent.stageY, true ) )
					{
						closeCommunitiesPopup();
					}
				}
			}
			
			protected function positionCommunitiesPopup():void
			{
				if ( communitiesPopup )
				{
					var p:Point = buttonsGroup.contentToGlobal( new Point( communitiesGroup.x, communitiesGroup.y ) );
					communitiesPopup.x = p.x - ( communitiesPopup.width / 2 ) + ( communitiesGroup.width / 2 );
					communitiesPopup.y = p.y + communitiesGroup.height + 2;
				}
			}
			
			/**
			 * function to handle when a community is selected
			 * 
			 * @param event The CommunityEvent received
			 */
			protected function handleSelectCommunityEvent(event:CommunityEvent):void
			{
				sourceData.removeAll();
				currsourceData.removeAll();
				if(event.selectedCommunities != null && event.selectedCommunities.length > 0)
				{
					selectedCommunities = event.selectedCommunities;
					loadSourceData(event.selectedCommunities);
				}
			}
			
			/**
			 * function to handle refreshing of the data
			 * 
			 * @param event The MouseEvent received
			 */
			protected function refreshButton_clickHandler(event:MouseEvent):void
			{
				refreshButton.selected = false;
				sourceData.removeAll();
				currsourceData.removeAll();
				loadSourceData(selectedCommunities);
			}
			
			/**
			 * funciton to clear the data in the provider
			 * for the grid
			 */
			public function clearSources():void
			{
				sourceData.removeAll();
				currsourceData.removeAll();
			}
			
			/**
			 * function to check if a filter has been 
			 * previously applied
			 */
			private function checkFilter():void
			{
				if(filterInput.text.length > 0)
					filter_enterHandler(null);
			}
			
			/**
			 * This method checks to see if one item is in a collection.  The id is used
			 * to do the matching.
			 *
			 * @param collection The collection containing the original item
			 * @param item The updated item
			 * @param idField The field used to match the items
			 * 
			 * @return If the collection contains the item or not
			 */
			public static function doesCollectionContainItem( collection:IList, item:Object, idField:String = "_id" ):Boolean
			{
				var currentItem:Object;
				
				if ( collection && item )
				{
					for ( var i:uint = 0; i < collection.length; i++ )
					{
						currentItem = collection.getItemAt( i );
						
						if ( currentItem.hasOwnProperty( idField ) && item.hasOwnProperty( idField ) && currentItem[ idField ] == item[ idField ] )
						{
							return true;
						}
					}
				}
				return false;
			}
			
			/**
			 * This method replaces one item in a collection with an updated version.  The id is used
			 * to do the matching.
			 *
			 * @param collection The collection containing the original item
			 * @param item The updated item
			 * @param idField The field used to match the items
			 */
			public static function replaceItemById( collection:IList, item:Object, idField:String = "_id" ):void
			{
				var currentItem:Object;
				
				if ( collection && item )
				{
					var len:int = collection.length;
					
					for ( var i:uint = 0; i < len; i++ )
					{
						currentItem = collection.getItemAt( i );
						
						if ( currentItem[ idField ] == item[ idField ] )
						{
							collection.setItemAt( item, i );
							return;
						}
					}
				}
				return;
			}
		]]>
	</fx:Script>
	<fx:Declarations>
		<!-- Place non-visual elements (e.g., services, value objects) here -->
		<s:HTTPService id="goodSourceService"
					   fault="goodSourceService_faultHandler(event)"
					   result="goodSourceService_resultHandler(event)" />
		<s:HTTPService id="pendingSourceService"
					   fault="pendingSourceService_faultHandler(event)"
					   result="pendingSourceService_resultHandler(event)" />
		<s:Fade id="debugFadeOut"
				alphaFrom="1"
				alphaTo="0"
				duration="3000" />
		<s:Fade id="debugFadeIn"
				alphaFrom="0"
				alphaTo="1"
				duration="1000" />
	</fx:Declarations>
	
	<s:HGroup id="buttonsGroup"
		paddingLeft="5"
		verticalAlign="middle">
		<views:InfLabel id="title"
			 text="Search: "
			 styleName="lightGrey"
			 left="0"
			 verticalCenter="2" />
		<views:FilterTextInput id="filterInput"
			styleName="filterTextInput"
			width="220"
			text="{ searchTerm }"
			change="filter_enterHandler(event)" 
			clear="filterTextInput_clearHandler()"
			focusIn="filterTextInput_focusInHandler()"
			prompt="Filter Sources"
			resultsFound="{ currsourceData.length != 0 }"
			showFilterButton="false" />
		<s:Spacer width="20"/>
		<s:HGroup id="communitiesGroup">
			<views:InfToggleButton id="communitiesButton"
					label="Community"
					styleName="communityButton"
					click="communitiesButton_clickHandler(event)" />
			<views:InfToggleButton id="refreshButton"
					label="Refresh"
					styleName="refreshButton"
					click="refreshButton_clickHandler(event)"
					width="60"/>
		</s:HGroup>
		<s:Spacer width="60" />
		<views:InfLabel id="debugLabel"
						width="100%"
						styleName="lightGrey"
						left="0"
						verticalCenter="2"
						hideEffect="{ debugFadeOut }"
						showEffect="{ debugFadeIn }"
						visible="false" />
	</s:HGroup>
	
	<!--itemDoubleClick="statusDG_itemDoubleClickHandler(event)"-->
	<s:BorderContainer width="100%" height="100%" borderColor="0x000000">
		<views:InfSelectableDataGrid id="statusDG"
									 width="100%"
									 height="100%"
									 horizontalScrollPolicy="off"
									 dataProvider="{ currsourceData }"
									 doubleClickEnabled="true"
									 doubleClick="statusDG_itemDoubleClickHandler(event)">
			<views:columns>
				<s:ArrayList>
					<s:GridColumn 
						width="70"
						dataField="statusColor"
						headerText="Status"
						headerRenderer="renderers.GridTextHeaderRenderer" 
						itemRenderer="renderers.StatusColorDGItemRenderer"
						resizable="false"/>
					<s:GridColumn
						width="170"
						dataField="title"
						headerText="Title"
						headerRenderer="renderers.GridTextHeaderRenderer" 
						itemRenderer="renderers.GridTextItemRenderer"/>
					<s:GridColumn 
						width="170"
						dataField="_id"
						headerText="ID"
						headerRenderer="renderers.GridTextHeaderRenderer" 
						itemRenderer="renderers.GridTextItemRenderer" />
					<s:GridColumn 
						width="170"
						dataField="communityName"
						headerText="Community"
						headerRenderer="renderers.GridTextHeaderRenderer" 
						itemRenderer="renderers.GridTextItemRenderer" />
					<s:GridColumn
						width="170"
						dataField="communityID"
						headerText="Community ID"
						headerRenderer="renderers.GridTextHeaderRenderer" 
						itemRenderer="renderers.GridTextItemRenderer" />
					<s:GridColumn
						width="100"
						dataField="isApproved"
						headerText="Approved"
						headerRenderer="renderers.GridTextHeaderRenderer" 
						itemRenderer="renderers.GridTextItemRenderer" />
					<s:GridColumn 
						width="100"
						dataField="harvest.harvest_status"
						headerText="Harvest Status"
						headerRenderer="renderers.GridTextHeaderRenderer" 
						itemRenderer="renderers.GridTextItemRenderer"
						sortCompareFunction="statusCompareFunction" />
					<s:GridColumn
						width="90"
						dataField="harvest.doccount"
						headerText="Doc Count"
						headerRenderer="renderers.GridTextHeaderRenderer" 
						itemRenderer="renderers.GridTextItemRenderer"
						sortCompareFunction="docCountCompareFunction" />
					<s:GridColumn 
						width="155"
						dataField="harvest.harvested"
						headerText="Last Harvest Date"
						headerRenderer="renderers.GridTextHeaderRenderer" 
						itemRenderer="renderers.GridTextItemRenderer"
						sortCompareFunction="dateCompareFunction" />
					<s:GridColumn
						dataField="harvest.harvest_message"
						editable="true"
						headerText="Harvest Error Message"
						headerRenderer="renderers.GridTextHeaderRenderer" 
						itemRenderer="errorLabelItemRenderer"
						sortCompareFunction="messageCompareFunction" />
				</s:ArrayList>	
			</views:columns>
		</views:InfSelectableDataGrid>
	</s:BorderContainer>
</s:VGroup>
